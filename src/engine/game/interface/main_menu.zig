//! The front end's main menu, its screen 0 (`main_menu`, `0x00428B60`), drawn by `main_menu_draw`
//! (`0x004291C0`), the render hook it puts in `sr + 0x88`.
//!
//! **Unverified:** the file. The menu lies between GenILib's `interf.cpp` and `interface.cpp`'s
//! known code; it is one of the game's screens rather than what runs them, so it goes with
//! `interface.cpp`.

const std = @import("std");
const assert = std.debug.assert;

const fat = @import("../../../formats/fat.zig");
const input = @import("../../input.zig");
const create = @import("../create.zig");
const gameobj = @import("../gameobj.zig");
const hog_snd = @import("../hog_snd.zig");
const hud = @import("../hud.zig");
const pilots = @import("../pilots.zig");
const canvas_module = @import("canvas.zig");
const Canvas = canvas_module.Canvas;
const Pointer = canvas_module.Pointer;
const Label = canvas_module.Label;
const Rect = canvas_module.Rect;
const dialog = @import("dialog.zig");

/// What the menu shows behind itself (`background_set`), its shapes (`interface_shapes`), and the
/// music it starts when none is playing, at 127.
pub const background_name = "interface\\sl_splash2.tga";
pub const shapes_name = "interface\\frontend.spr";
pub const music_name = "music\\New_Pensive.wav";
pub const music_level = 0x7F;

/// The menu's items, in the order of its table, then OpenReliant's GAME MODES, which is there only
/// while the mods' scripts have registered game modes (`Context.game_modes`).
pub const Item = enum(u3) { single_player, multi_player, game_options, quit, instant_action, game_modes };

/// A hotspot of the menu (`main_menu_hotspots`, `0x004E5B90`): where the pointer finds its item,
/// what choosing it returns, and the shape a panel shows under the pointer. The buttons' shape goes
/// unused: the drawing lights them with `lit_button_shape`.
pub const Hotspot = extern struct {
    rect: Rect,
    result: Result,
    shape: i16,

    /// What choosing an item returns: its screen, or first the question whether to quit.
    pub const Result = enum(i16) {
        go = 0,
        quit = 3,
        _,
    };
};

comptime {
    assert(@sizeOf(Hotspot) == 12);
}

pub const hotspots = std.EnumArray(Item, Hotspot).init(.{
    .single_player = .{ .rect = .{ .x = 27, .y = 123, .width = 184, .height = 290 }, .result = .go, .shape = 18 },
    .multi_player = .{ .rect = .{ .x = 203, .y = 125, .width = 184, .height = 290 }, .result = .go, .shape = 19 },
    .game_options = .{ .rect = .{ .x = 421, .y = 165, .width = 184, .height = 290 }, .result = .go, .shape = 20 },
    .quit = .{ .rect = .{ .x = 332, .y = 441, .width = 20, .height = 15 }, .result = .quit, .shape = 24 },
    .instant_action = .{ .rect = .{ .x = 300, .y = 441, .width = 20, .height = 15 }, .result = .go, .shape = 24 },
    .game_modes = .{ .rect = .{ .x = game_modes_button[0], .y = game_modes_button[1], .width = 20, .height = 15 }, .result = .go, .shape = 24 },
});

/// A panel's two lines, in the large font, centred under it; QUIT and INSTANT ACTION have none.
fn panelLabels(item: Item) ?[2]Label {
    return switch (item) {
        .single_player => .{ .of(0xBD, .{ 0x74, 0x154 }, .centre), .of(0xBF, .{ 0x74, 0x164 }, .centre) },
        .multi_player => .{ .of(0x5B4, .{ 0x140, 0x154 }, .centre), .of(0x5B5, .{ 0x140, 0x164 }, .centre) },
        .game_options => .{ .of(0xC0, .{ 0x20C, 0x154 }, .centre), .of(0xC1, .{ 0x20C, 0x164 }, .centre) },
        .quit, .instant_action, .game_modes => null,
    };
}

/// QUIT and INSTANT ACTION (`main_menu_draw`): their buttons' shapes, lit under the pointer, and
/// their labels in the small font, to the button's right and left.
const button_shape = 0x1B;
const lit_button_shape = 0x1C;
const quit_button: [2]i32 = .{ 0x14C, 0x1B9 };
const instant_action_button: [2]i32 = .{ 0x12C, 0x1B9 };
const quit_label: Label = .of(0xBC, .{ 0x168, 0x1B7 }, .left);
const instant_action_label: Label = .of(0x288, .{ 0x128, 0x1B7 }, .right);

/// OpenReliant's GAME MODES (`game_modes`): a button as QUIT's and INSTANT ACTION's are, in the
/// same row at the screen's left, its label to its right.
const game_modes_button: [2]i32 = .{ 40, 0x1B9 };
const game_modes_label: Label = .{ .text = .{ .words = "GAME MODES" }, .at = .{ game_modes_button[0] + 0x1C, 0x1B7 }, .alignment = .left };

/// The question QUIT asks (`0x00428EFF`): Do you really want to Quit?
pub const quit_question = 0x374;

/// OpenReliant's notice of a newer release, with its version, which YES answers by opening the
/// release's page.
const release_question = "A new version of OpenReliant, {s}, is available. Open its download page?";

/// The longest version the notice shows (`Release.pending`), and the room its words take.
pub const max_version = 32;
const release_question_size = release_question.len + max_version;

/// OpenReliant's: a newer release of OpenReliant than the one playing, which the menu shows the
/// player once its check has found it (`openreliant/updates.zig`).
///
/// **Improvement:** the original never looks for a newer version of itself.
pub const Release = struct {
    context: *anyopaque,
    vtable: *const VTable,

    pub const VTable = struct {
        /// The version of a newer release the menu hasn't shown yet, such as 0.10.0, at most
        /// `max_version` characters.
        pending: *const fn (context: *anyopaque) ?[]const u8,
        /// The player answered the notice: `open` whether they asked for the release's page.
        answered: *const fn (context: *anyopaque, open: bool) void,
    };

    pub fn pending(release: Release) ?[]const u8 {
        return release.vtable.pending(release.context);
    }

    pub fn answered(release: Release, open: bool) void {
        release.vtable.answered(release.context, open);
    }
};

/// The click, a sound of `bank_stdsmp`, as the item chosen plays it: at 127, panned to the middle.
const click_sound = 0xB;
const click_volume = 0x7F;
const click_pan = 0x40;

/// The developers' code (`0x00428B71`): POTATO, each letter typed with Control, which turns on the
/// developers' keys (`developer_mode`, `0x005D5641`) until the game quits.
const code = [_]input.Key{ .p, .o, .t, .a, .t, .o };

/// With the developers' keys on: the mission they start is written at the top left, `M` and its
/// number, in red in `font_01.fnt` (`font_01`, `0x0059506C`).
pub const developer_font_name = "font_01.fnt";
const developer_text_at: [2]i32 = .{ 5, 5 };

/// The developers' keys that start a mission without its briefing, with Shift, and the ship type
/// each gives the player: F1 to F10 the first ten, then F11 and F12.
const ship_keys = [_]input.Key{ .f1, .f2, .f3, .f4, .f5, .f6, .f7, .f8, .f9, .f10, .f11, .f12 };

/// The number keys, which type the digits 1 to 9 and 0 of the developers' mission.
const digit_keys = [_]input.Key{ .one, .two, .three, .four, .five, .six, .seven, .eight, .nine, .zero };

/// Where the menu leads.
pub const Choice = union(enum) {
    /// SINGLE PLAYER's screen, the pilot roster, MULTI PLAYER's, the connection, and GAME
    /// OPTIONS'.
    pilot_roster,
    connection,
    game_options,
    /// INSTANT ACTION (`instant_action`).
    instant_action,
    /// OpenReliant's GAME MODES screen (`game_modes`).
    game_modes,
    /// QUIT, answered YES.
    quit,
    /// A mission the developers' keys start, without its briefing (`skip_briefing`).
    fly: Flight,
    /// The developers' Enter with Control: the briefing of the mission their keys name, from its
    /// loadout on (`briefing_from_loadout`, `0x0051DB48`), the front end's screen 7 next.
    briefing: u16,
};

/// A mission to fly: its number, the ship type the loadout gives the player, or null for the ship
/// the mission gives, the simulator it runs in, and what flies it.
pub const Flight = struct {
    /// The number the mission is flown as, which the game's rules go by.
    mission: u16,
    /// OpenReliant's: the number of the file it is read from, `mission<file>.dte`, where a game
    /// mode flies a mod's mission as one of the game's numbers; `mission`'s own where null.
    file: ?u16 = null,
    /// OpenReliant's: the names a game mode gives its objectives (`hud.Objectives.reset`).
    objectives: ?*const hud.Objectives.Names = null,
    /// OpenReliant's: the pilots a game mode seats in the player's wing (`pilots.Wingmen.seat`).
    wing: []const pilots.Number = &.{},
    ship: ?create.TypeIndex = null,
    /// The racks the loadout fitted the ship with, where it ran (`player_loadouts + 4` on); none
    /// where the mission starts without it, when the player's ship is fitted by its tier
    /// (`skip_briefing`, `0x005883B4`).
    racks: ?create.Racks = null,
    simulator: create.Simulator = .{},
    flier: Flier = .winmain,

    /// What flies a mission, and so what follows it.
    pub const Flier = enum {
        /// `WinMain`, with the hangar's movie before it and the landing after
        /// (`xtrabits.movie.Hangar`, `xtrabits.landing`).
        winmain,
        /// The main menu, INSTANT ACTION's, which comes back to itself.
        main_menu,
        /// The simulator pod, which comes back to itself (`interface.loadout.simulator_pod`).
        simulator_pod,
    };

    /// Whether `WinMain` flies it.
    pub fn byWinMain(flight: Flight) bool {
        return flight.flier == .winmain;
    }
};

/// INSTANT ACTION (`0x0042905B` to `0x004290E4`): mission 29 in a Grendel, ship type 2, in the
/// simulator of its mode, which the main menu flies again for as long as RESTART starts it again,
/// then comes back to itself. It keeps the mission's number and the pilot's kills as they were.
pub const instant_action: Flight = .{
    .mission = create.instant_action_mission,
    .ship = @backingInt(gameobj.GameType.grendel),
    .simulator = .{ .mode = .instant_action, .instant_action = true },
    .flier = .main_menu,
};

/// What a frame of the menu reads and plays through.
pub const Context = struct {
    pointer: Pointer,
    keyboard: *input.Keyboard,
    sound: ?*hog_snd.Sound = null,
    /// `bank_stdsmp`, which the click plays from.
    bank: ?fat.Bank = null,
    /// Whether the mods' scripts have registered game modes, which GAME MODES leads to.
    game_modes: bool = false,
    /// OpenReliant's: the newer release the menu shows the player, if any.
    release: ?Release = null,
};

/// The menu's state.
pub const MainMenu = struct {
    /// The item under the pointer (`0x0051D544`).
    under: ?Item = null,
    /// How much of the developers' code has been typed.
    typed: usize = 0,
    /// Whether the developers' keys are on (`0x005D5641`).
    developer: bool = false,
    /// The mission the developers' keys start (`mission_number`), whose number they type.
    mission: u16 = 1,
    /// QUIT's question, or OpenReliant's notice of a newer release, while it is up.
    confirm: ?dialog.Confirm = null,
    /// The version of the newer release the dialog shows, where it shows one rather than asking
    /// whether to quit.
    release: ?[]const u8 = null,
    /// Whether it shows GAME MODES, as the last pass found (`Context.game_modes`).
    has_modes: bool = false,

    /// Entering the menu, as `main_menu` does before its loop: the pointer at (320, 200), and the
    /// music started where none is playing. The game also starts a new campaign there
    /// (`campaign_new`, `0x00428C33`). OpenReliant reads the pilot's profile again as the menu is
    /// entered, as that does (`gameflow.ProfileFile.open`), and starts every mission the front end
    /// flies from a new campaign's variables (`gameflow.restartPoint`).
    pub fn enter(menu: *MainMenu, pointer: *Pointer, sound: ?*hog_snd.Sound) void {
        menu.under = null;
        menu.confirm = null;
        menu.release = null;
        pointer.at = .{ 320, 200 };
        if (sound) |playing| if (!playing.musicPlaying()) playing.playMusic(music_name, 0, music_level, .now);
    }

    /// A pass of `main_menu`'s loop: Escape, or QUIT, asks whether to quit, and while the
    /// question is up it takes the frame (`interface_confirm`). Otherwise the developers' code and
    /// keys, then the item under the pointer, chosen while the pointer's button is down, with a
    /// click.
    ///
    /// OpenReliant's: while no dialog is up, a newer release the menu hasn't shown yet comes first,
    /// in the same dialog, once the pointer's button is up, so that a click under way doesn't
    /// answer it. YES opens the release's page, and either answer closes it for good.
    pub fn frame(menu: *MainMenu, context: Context) ?Choice {
        const keyboard = context.keyboard;
        const escaped = keyboard.pressed(input.scan.escape, .none, true);
        if (menu.confirm) |*confirm| {
            const answer = confirm.frame(context.pointer, escaped) orelse return null;
            menu.confirm = null;
            if (menu.release == null) return if (answer) .quit else null;
            menu.release = null;
            if (context.release) |release| release.answered(answer);
            return null;
        }
        if (context.release) |release| if (!context.pointer.down) if (release.pending()) |version| {
            menu.release = version;
            // The words are put together as the notice is drawn.
            menu.confirm = .{ .message = .{ .words = "" } };
            return null;
        };
        if (escaped) {
            menu.confirm = .{ .message = .{ .string = quit_question } };
            return null;
        }
        if (menu.typed < code.len and keyboard.pressed(@backingInt(code[menu.typed]), .control, true)) {
            menu.typed += 1;
            if (menu.typed == code.len) menu.developer = true;
        }
        if (menu.developer) if (menu.developerKeys(keyboard)) |choice| return choice;

        menu.has_modes = context.game_modes;
        menu.under = for (std.enums.values(Item)) |item| {
            if (!menu.shows(item)) continue;
            if (hotspots.get(item).rect.holds(context.pointer.at)) break item;
        } else null;
        const chosen = menu.under orelse return null;
        if (!context.pointer.down) return null;
        if (context.sound) |sound| if (context.bank) |bank| {
            _ = sound.play(bank, click_sound, click_volume, 1, click_pan, 0);
        };
        if (hotspots.get(chosen).result == .quit) {
            menu.confirm = .{ .message = .{ .string = quit_question } };
            return null;
        }
        return switch (chosen) {
            .single_player => .pilot_roster,
            .multi_player => .connection,
            .game_options => .game_options,
            .instant_action => .instant_action,
            .game_modes => .game_modes,
            .quit => unreachable,
        };
    }

    /// Whether the menu shows `item`: GAME MODES only while there are game modes.
    fn shows(menu: MainMenu, item: Item) bool {
        return item != .game_modes or menu.has_modes;
    }

    /// The developers' keys (`0x00428C9B` on): Enter with Shift starts the mission without its
    /// briefing in the first ship type, as Shift with F1 does, and with Control leads to its
    /// briefing from the loadout on; Shift and F1 to F12 start it without its briefing, in the
    /// key's ship type. A number key types a digit of the mission's number: after a single digit it
    /// adds one, after two it starts again.
    fn developerKeys(menu: *MainMenu, keyboard: *input.Keyboard) ?Choice {
        const enter_key = @backingInt(input.Key.enter);
        if (keyboard.pressed(enter_key, .shift, true)) return .{ .fly = .{ .mission = menu.mission, .ship = 0 } };
        if (keyboard.pressed(enter_key, .control, true)) return .{ .briefing = menu.mission };
        for (ship_keys, 0..) |key, ship| {
            if (keyboard.pressed(@backingInt(key), .shift, true)) return .{ .fly = .{ .mission = menu.mission, .ship = @intCast(ship) } };
        }
        for (digit_keys, 1..) |key, value| {
            if (!keyboard.pressed(@backingInt(key), .none, true)) continue;
            const digit: u16 = @intCast(value % 10);
            menu.mission = if (menu.mission < 10) menu.mission * 10 + digit else digit;
            break;
        }
        return null;
    }

    /// `main_menu_draw`: the panels' labels in blue, QUIT's and INSTANT ACTION's buttons and labels,
    /// the item under the pointer lit and a panel's labels in gold, QUIT's question or the notice
    /// of a newer release where it is up, then the pointer, and with the developers' keys on the
    /// mission's number. The menu's background is drawn behind it all (`background_set`).
    pub fn draw(menu: MainMenu, canvas: Canvas, art: *hud.Art, dialog_art: *hud.Art, pointer: Pointer, developer_font: ?*hud.Opened) canvas_module.Error!void {
        const large = canvas.fonts.large;
        const small = canvas.fonts.small;
        for (std.enums.values(Item)) |item| {
            for (panelLabels(item) orelse continue) |label| try label.write(canvas, large, canvas_module.blue);
        }
        try quit_label.write(canvas, small, canvas_module.blue);
        try instant_action_label.write(canvas, small, canvas_module.blue);
        try canvas.shape(art, button_shape, quit_button);
        try canvas.shape(art, button_shape, instant_action_button);
        if (menu.has_modes) {
            try game_modes_label.write(canvas, small, canvas_module.blue);
            try canvas.shape(art, button_shape, game_modes_button);
        }
        if (menu.under) |under| {
            switch (under) {
                .single_player, .multi_player, .game_options => {
                    const hotspot = hotspots.get(under);
                    try canvas.shape(art, @intCast(hotspot.shape), .{ hotspot.rect.x, hotspot.rect.y });
                    for (panelLabels(under).?) |label| try label.write(canvas, large, canvas_module.gold);
                },
                .quit => try canvas.shape(art, lit_button_shape, quit_button),
                .instant_action => try canvas.shape(art, lit_button_shape, instant_action_button),
                .game_modes => try canvas.shape(art, lit_button_shape, game_modes_button),
            }
        }
        if (menu.confirm) |confirm| {
            var asked = confirm;
            var buffer: [release_question_size]u8 = undefined;
            if (menu.release) |version| asked.message = .{ .words = std.mem.print(&buffer, release_question, .{version}) catch "" };
            try asked.draw(canvas, dialog_art);
        }
        try canvas.drawVersion();
        try canvas.onScreen().shape(art, pointer.shape(), pointer.at);
        if (menu.developer) if (developer_font) |font| {
            var buffer: [16]u8 = undefined;
            const number = std.mem.print(&buffer, "M{d}", .{menu.mission}) catch return;
            try canvas.text(font, developer_text_at, number, canvas_module.red, .left);
        };
    }
};

test "an item is found under the pointer, and chosen while the button is down" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 100, 200 } }, .keyboard = &keyboard }));
    try std.testing.expectEqual(.single_player, menu.under);
    try std.testing.expectEqual(Choice.pilot_roster, menu.frame(.{ .pointer = .{ .at = .{ 100, 200 }, .down = true }, .keyboard = &keyboard }).?);
    try std.testing.expectEqual(Choice.game_options, menu.frame(.{ .pointer = .{ .at = .{ 500, 300 }, .down = true }, .keyboard = &keyboard }).?);
    try std.testing.expectEqual(Choice.instant_action, menu.frame(.{ .pointer = .{ .at = .{ 310, 450 }, .down = true }, .keyboard = &keyboard }).?);
    // Nothing under the pointer, nothing chosen.
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 5, 5 }, .down = true }, .keyboard = &keyboard }));
    try std.testing.expectEqual(null, menu.under);
}

test "GAME MODES is there only while there are game modes" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    const at: [2]i32 = .{ game_modes_button[0] + 5, game_modes_button[1] + 5 };
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = at, .down = true }, .keyboard = &keyboard }));
    try std.testing.expectEqual(Choice.game_modes, menu.frame(.{ .pointer = .{ .at = at, .down = true }, .keyboard = &keyboard, .game_modes = true }).?);
}

test "QUIT asks first" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 340, 450 }, .down = true }, .keyboard = &keyboard }));
    try std.testing.expect(menu.confirm != null);
    // YES, once the button comes up, quits.
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 295, 275 }, .down = true }, .keyboard = &keyboard }));
    try std.testing.expectEqual(Choice.quit, menu.frame(.{ .pointer = .{ .at = .{ 295, 275 } }, .keyboard = &keyboard }).?);
}

/// A newer release found by a check, for the tests, and how the player answered.
const FoundRelease = struct {
    version: ?[]const u8 = "0.10.0",
    answer: ?bool = null,

    fn release(found: *FoundRelease) Release {
        return .{ .context = found, .vtable = &.{ .pending = pending, .answered = answered } };
    }

    fn pending(context: *anyopaque) ?[]const u8 {
        const found: *FoundRelease = @ptrCast(@alignCast(context));
        return found.version;
    }

    fn answered(context: *anyopaque, open: bool) void {
        const found: *FoundRelease = @ptrCast(@alignCast(context));
        found.answer = open;
        found.version = null;
    }
};

test "a newer release is shown once, and YES opens its page" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    var found: FoundRelease = .{};
    // Not while the pointer's button is down, so that a click under way doesn't answer it.
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 5, 5 }, .down = true }, .keyboard = &keyboard, .release = found.release() }));
    try std.testing.expectEqual(null, menu.confirm);
    // With the button up, the notice comes up, and takes the frame.
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 5, 5 } }, .keyboard = &keyboard, .release = found.release() }));
    try std.testing.expectEqualStrings("0.10.0", menu.release.?);
    // YES, once the button comes up, opens the page, and the menu goes on without quitting.
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 295, 275 }, .down = true }, .keyboard = &keyboard, .release = found.release() }));
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 295, 275 } }, .keyboard = &keyboard, .release = found.release() }));
    try std.testing.expectEqual(true, found.answer.?);
    try std.testing.expect(menu.confirm == null and menu.release == null);
    // Shown once, it doesn't come again, and QUIT asks as before.
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 340, 450 }, .down = true }, .keyboard = &keyboard, .release = found.release() }));
    try std.testing.expect(menu.confirm != null and menu.release == null);
}

test "Escape closes the notice of a newer release" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    var found: FoundRelease = .{};
    _ = menu.frame(.{ .pointer = .{}, .keyboard = &keyboard, .release = found.release() });
    keyboard.down[input.scan.escape] = true;
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{}, .keyboard = &keyboard, .release = found.release() }));
    try std.testing.expectEqual(false, found.answer.?);
    try std.testing.expect(menu.confirm == null);
}

test "Escape asks to quit, and NO goes back to the menu" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    keyboard.down[input.scan.escape] = true;
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{}, .keyboard = &keyboard }));
    try std.testing.expect(menu.confirm != null);
    keyboard.down[input.scan.escape] = false;
    _ = menu.frame(.{ .pointer = .{ .at = .{ 340, 275 }, .down = true }, .keyboard = &keyboard });
    try std.testing.expectEqual(null, menu.frame(.{ .pointer = .{ .at = .{ 340, 275 } }, .keyboard = &keyboard }));
    try std.testing.expectEqual(null, menu.confirm);
}

test "the developers' code and keys" {
    var keyboard: input.Keyboard = .{};
    var menu: MainMenu = .{};
    const control = @backingInt(input.Key.left_control);
    keyboard.down[control] = true;
    for (code) |letter| {
        const key = @backingInt(letter);
        keyboard.down[key] = true;
        _ = menu.frame(.{ .pointer = .{}, .keyboard = &keyboard });
        keyboard.down[key] = false;
        keyboard.latched[key] = false;
    }
    keyboard.down[control] = false;
    try std.testing.expect(menu.developer);
    // Typing 1 then 5 picks mission 15.
    menu.mission = 0;
    for ([_]input.Key{ .one, .five }) |digit| {
        const key = @backingInt(digit);
        keyboard.down[key] = true;
        _ = menu.frame(.{ .pointer = .{}, .keyboard = &keyboard });
        keyboard.down[key] = false;
        keyboard.latched[key] = false;
    }
    try std.testing.expectEqual(15, menu.mission);
    // Shift and F3 start it without its briefing in ship type 2.
    keyboard.down[@backingInt(input.Key.left_shift)] = true;
    keyboard.down[@backingInt(input.Key.f3)] = true;
    const flight = menu.frame(.{ .pointer = .{}, .keyboard = &keyboard }).?.fly;
    try std.testing.expectEqual(Flight{ .mission = 15, .ship = 2 }, flight);
    try std.testing.expect(flight.byWinMain());
    try std.testing.expect(!instant_action.byWinMain());
    keyboard.down[@backingInt(input.Key.f3)] = false;
    // Shift and Enter start it without its briefing in ship type 0; Control and Enter lead to its
    // briefing from the loadout on.
    const enter = @backingInt(input.Key.enter);
    keyboard.down[enter] = true;
    try std.testing.expectEqual(Flight{ .mission = 15, .ship = 0 }, menu.frame(.{ .pointer = .{}, .keyboard = &keyboard }).?.fly);
    keyboard.down[enter] = false;
    keyboard.latched[enter] = false;
    keyboard.down[@backingInt(input.Key.left_shift)] = false;
    keyboard.down[@backingInt(input.Key.left_control)] = true;
    keyboard.down[enter] = true;
    try std.testing.expectEqual(15, menu.frame(.{ .pointer = .{}, .keyboard = &keyboard }).?.briefing);
}
